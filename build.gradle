plugins {
    id 'java'
    id 'eclipse'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.gradleup.shadow' version '9.0.0'
}

group = 'com.znzlspt'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

allprojects {
    group = 'com.znzlspt17'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    
    // ⭐ 이 부분 추가
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
    
    // Spring Boot jar 생성 및 실행 비활성화 (server 모듈만 실행 가능)
    tasks.named('bootJar') {
        enabled = false
    }
    
    tasks.named('bootRun') {
        enabled = false
    }
    
    tasks.named('jar') {
        enabled = true
    }
}

// server 모듈만 bootRun 활성화
project(':server') {
    tasks.named('bootRun') {
        enabled = true
        mainClass = 'com.znzlspt.server.Main'
    }
}

// 루트 프로젝트는 실행 가능한 jar를 생성하지 않음
bootJar {
    enabled = false
}

jar {
    enabled = false
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation project(':netcore')
    implementation project(':server')
}

tasks.named('shadowJar') {
    archiveBaseName = 'server'
    archiveClassifier = 'jar-with-dependencies'

    manifest {
        attributes(
            'Main-Class': 'com.znzlspt.server.Main'
        )
    }

    mergeServiceFiles()

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('build') {
    dependsOn tasks.shadowJar
}